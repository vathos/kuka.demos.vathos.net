&ACCESS RVO
&PARAM EDITMASK = *
DEFFCT BOOL handeye_calibration(product: IN, session: IN, on_arm: IN, result: OUT)
   
   DECL EKI_STATUS ret
   DECL INT status
   DECL INT i
   DECL CHAR product[]
   DECL CHAR session[]
   DECL BOOL on_arm
   DECL REAL result_array[6]
   DECL FRAME result

   ; init
   status = 3
   FOR i=1 TO 6
      result_array[i] = 0.0
   ENDFOR
   
   ret = EKI_Init("handeye_calibration")
   ret = EKI_Open("handeye_calibration")
   WAIT FOR $FLAG[10]
   
   ret = EKI_SetString("handeye_calibration", "request/method", "handeye_calibration")
   ret = EKI_SetString("handeye_calibration", "request/params/session/type", "string")
   ret = EKI_SetString("handeye_calibration","request/params/session/value", session[])
   ret = EKI_SetString("handeye_calibration", "request/params/product_id/type", "string")
   ret = EKI_SetString("handeye_calibration","request/params/product_id/value", product[])
   ret = EKI_SetString("handeye_calibration", "request/params/representation/type", "string")
   ret = EKI_SetString("handeye_calibration", "request/params/representation/value", "ZYX")
   ret = EKI_SetString("handeye_calibration", "request/params/degrees/type", "bool")
   ret = EKI_SetBool("handeye_calibration", "request/params/degrees/value", TRUE)
   ret = EKI_SetString("handeye_calibration", "request/params/eye_in_hand/type", "bool")
   ret = EKI_SetBool("handeye_calibration", "request/params/eye_in_hand/value", on_arm)
   ret = EKI_Send("handeye_calibration", "request")
   
   ; parse response
   WAIT FOR $FLAG[1]
   ret = EKI_GetInt("handeye_calibration","response/status", status)   
   
   ; if there are no get_detections, close socket and return at once
   IF status>0 THEN
      ; close network socket
      ret = EKI_Close("handeye_calibration")
      ret = EKI_Clear("handeye_calibration")
      RETURN FALSE   
   ENDIF
   
   WAIT FOR $FLAG[2]
   ret = EKI_GetRealArray("handeye_calibration", "response/params/param", result_array[])   
   
   ; KUKA poses are in mm
   result.X = result_array[1] * 1000
   result.Y = result_array[2] * 1000
   result.Z = result_array[3] * 1000
   result.A = result_array[4]
   result.B = result_array[5]
   result.C = result_array[6]
   
   ; close socket
   ret = EKI_Close("handeye_calibration")
   ret = EKI_Clear("handeye_calibration")
   
   RETURN TRUE
   
ENDFCT
&ACCESS RVO1
&PARAM EDITMASK = *
DEFFCT BOOL reset_layer()
   
   DECL EKI_STATUS ret
   DECL INT status
   status = 3
   
   ; open network socket
   ret = EKI_Init("reset_layer")
   ret = EKI_Open("reset_layer")
   WAIT FOR $FLAG[10]
   
   ; assemble the XML message
   ret = EKI_SetString("reset_layer", "request/method", "call")
   ret = EKI_SetString("reset_layer", "request/params/function_name/type", "string")
   ret = EKI_SetString("reset_layer", "request/params/function_name/value", "reset_layer")
   ret = EKI_SetString("reset_layer", "request/params/workflow/type", "string")
   ret = EKI_SetString("reset_layer", "request/params/workflow/value", "palletizing")
   ret = EKI_SetString("reset_layer", "request/params/timeout/type", "int")
   ret = EKI_SetInt("reset_layer", "request/params/timeout/value", 5)
   
   ; send the request
   ret = EKI_Send("reset_layer", "request")

   ; wait for status
   WAIT FOR $FLAG[1]
   ret = EKI_GetInt("reset_layer","response/status", status)   

   ret = EKI_Close("reset_layer")
   ret = EKI_Clear("reset_layer")

   ; on failure, close socket and return at once
   IF status>0 THEN   
      RETURN FALSE   
   ELSE
      RETURN TRUE
   ENDIF
      
ENDFCT
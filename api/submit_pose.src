&ACCESS RVO
&PARAM EDITMASK = *
DEFFCT BOOL submit_pose(pallet_frame: IN, workflow: IN, class: IN)
   
   DECL EKI_STATUS ret
   DECL INT status
   DECL FRAME pallet_frame
   DECL FRAME pallet_frame_m
   DECL CHAR workflow[]
   DECL INT class
   
   ; init
   pallet_frame_m = $NULLFRAME
   status = 3
   
   ; KUKA poses are in mm, make a copy not to mess with input frame
   pallet_frame_m.X = pallet_frame.X * 0.001
   pallet_frame_m.Y = pallet_frame.Y * 0.001
   pallet_frame_m.Z = pallet_frame.Z * 0.001
   pallet_frame_m.A = pallet_frame.A
   pallet_frame_m.B = pallet_frame.B
   pallet_frame_m.C = pallet_frame.C
   
   ; open network socket
   ret = EKI_Init("submit_pose")
   ret = EKI_Open("submit_pose")
   WAIT FOR $FLAG[10]
   
   ; assemble request
   ret = EKI_SetString("submit_pose", "request/method", "call")
   ret = EKI_SetString("submit_pose", "request/params/function_name/type", "string")
   ret = EKI_SetString("submit_pose", "request/params/function_name/value", "submit_pose")
   ret = EKI_SetString("submit_pose", "request/params/workflow/type", "string")
   ret = EKI_SetString("submit_pose", "request/params/workflow/value", workflow[])
   ret = EKI_SetString("submit_pose", "request/params/timeout/type", "int")
   ret = EKI_SetInt("submit_pose", "request/params/timeout/value", 10)
   ret = EKI_SetString("submit_pose", "request/params/pose/type", "pose")
   ret = EKI_SetFrame("submit_pose","request/params/pose/value", pallet_frame_m)
   ret = EKI_SetString("submit_pose", "request/params/representation/type", "string")
   ret = EKI_SetString("submit_pose", "request/params/representation/value", "ZYX")
   ret = EKI_SetString("submit_pose", "request/params/degrees/type", "bool")
   ret = EKI_SetBool("submit_pose", "request/params/degrees/value", TRUE)
   ret = EKI_SetString("submit_pose", "request/params/class/type", "int")
   ret = EKI_SetInt("submit_pose", "request/params/class/value", class)
   
   ; send the request
   ret = EKI_Send("submit_pose", "request")
   
   WAIT FOR $FLAG[1]
   ret = EKI_GetInt("submit_pose", "response/status", status)   
   WAIT SEC 0.0
   
   ; close network socket
   ret = EKI_Close("submit_pose")
   ret = EKI_Clear("submit_pose")

   ; return FALSE on failure
   IF status>0 THEN   
      RETURN FALSE   
   ELSE
      RETURN TRUE
   ENDIF
        
ENDFCT
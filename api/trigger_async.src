&ACCESS RVO1
&REL 3
DEFFCT  BOOL trigger_async (wait_for_camera: IN)
   
   DECL BOOL wait_for_camera
   DECL EKI_STATUS ret
   DECL INT status
   status = 3
   
   ; open network socket
   ret = EKI_Init("trigger")
   ret = EKI_Open("trigger")
   WAIT FOR $FLAG[10]
   
   ; send request
   ret = EKI_SetString("trigger", "request/method", "trigger_async")
   ret = EKI_SetString("trigger", "request/params/workflow/type", "string")
   ret = EKI_SetString("trigger", "request/params/workflow/value", "votenet")
   ret = EKI_SetString("trigger", "request/params/wait_for_camera/type", "bool")
   ret = EKI_SetBool("trigger", "request/params/wait_for_camera/value", wait_for_camera)
   ret = EKI_Send("trigger", "request")
   
   ; check for errors
   WAIT FOR $FLAG[1]
   ret = EKI_GetInt("trigger","response/status", status)      
   
   ; close network socket
   ret = EKI_Close("trigger")
   ret = EKI_Clear("trigger")
   
   IF status>0 THEN
      RETURN FALSE
   ELSE
      RETURN TRUE
   ENDIF
   
ENDFCT
&ACCESS RVO
&REL 105
&PARAM EDITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM DISKPATH = KRC:\R1\Program
DEF handeye( )

   DECL CHAR timestamp[64]
   DECL STATE_T state
   DECL INT offset
   DECL FRAME result
   DECL BOOL api_call_success
  
   ;FOLD INI;%{PE}
      ;FOLD BASISTECH INI
         GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
         INTERRUPT ON 3 
         BAS (#INITMOV,0 )
      ;ENDFOLD (BASISTECH INI)
      ;FOLD USER INI
         ;Make your modifications here
         offset = 0
         result = $NULLFRAME
      ;ENDFOLD (USER INI)
   ;ENDFOLD (INI)
   
   ;FOLD SPTP HOME Vel=100 % DEFAULT ;%{PE}
      ;FOLD Parameters ;%{h}
         ;Params IlfProvider=kukaroboter.basistech.inlineforms.movement.spline; Kuka.IsGlobalPoint=False; Kuka.PointName=HOME; Kuka.BlendingEnabled=False; Kuka.MoveDataPtpName=DEFAULT; Kuka.VelocityPtp=100; Kuka.VelocityFieldEnabled=True; Kuka.CurrentCDSetIndex=0; Kuka.MovementParameterFieldEnabled=True; IlfCommand=SPTP
      ;ENDFOLD
      SPTP XHOME WITH $VEL_AXIS[1] = SVEL_JOINT(100.0), $TOOL = STOOL2(FHOME), $BASE = SBASE(FHOME.BASE_NO), $IPO_MODE = SIPO_MODE(FHOME.IPO_FRAME), $LOAD = SLOAD(FHOME.TOOL_NO), $ACC_AXIS[1] = SACC_JOINT(PDEFAULT), $APO = SAPO_PTP(PDEFAULT), $GEAR_JERK[1] = SGEAR_JERK(PDEFAULT), $COLLMON_TOL_PRO[1] = USE_CM_PRO_VALUES(0)
   ;ENDFOLD

   ; create a unique session id
   SWRITE(timestamp[], state, offset, "handeye_%04i%02i%02i_%02i:%02i", $DATE.YEAR, $DATE.MONTH, $DATE.DAY, $DATE.HOUR, $DATE.MIN)   
   MsgNotify(timestamp[], "timestamp.src")
   
   ; set session id in capture service
   api_call_success = patch_session("64e5d13a1d07739173a09311", timestamp[])
   IF NOT api_call_success THEN
      HALT
   ENDIF
   
   ; reload configuration
   api_call_success = load_configuration("63f8cb5fd312240012f27997", "calibration")
   IF NOT api_call_success THEN
      HALT
   ENDIF
   
   ; move to capture position where pattern is fully visible
   ;FOLD SPTP P1 Vel=100 % PDAT1 Tool[0] Base[2]:Handeye ;%{PE}
      ;FOLD Parameters ;%{h}
         ;Params IlfProvider=kukaroboter.basistech.inlineforms.movement.spline; Kuka.IsGlobalPoint=False; Kuka.PointName=P1; Kuka.BlendingEnabled=False; Kuka.MoveDataPtpName=PDAT1; Kuka.VelocityPtp=100; Kuka.VelocityFieldEnabled=True; Kuka.ColDetectFieldEnabled=True; Kuka.CurrentCDSetIndex=0; Kuka.MovementParameterFieldEnabled=True; IlfCommand=SPTP
      ;ENDFOLD
      SPTP XP1 WITH $VEL_AXIS[1] = SVEL_JOINT(100.0), $TOOL = STOOL2(FP2), $BASE = SBASE(FP2.BASE_NO), $IPO_MODE = SIPO_MODE(FP2.IPO_FRAME), $LOAD = SLOAD(FP2.TOOL_NO), $ACC_AXIS[1] = SACC_JOINT(PPDAT1), $APO = SAPO_PTP(PPDAT1), $GEAR_JERK[1] = SGEAR_JERK(PPDAT1), $COLLMON_TOL_PRO[1] = USE_CM_PRO_VALUES(0)
   ;ENDFOLD
   
   ; capture image
   api_call_success = save_image()
   IF NOT api_call_success THEN
      HALT
   ENDIF
   
   ; ...
   ; add 30 or more poses here for optimal calibration results
   ; ...

   ; run calibration
   api_call_success = handeye_calibration("63f8cb5fd312240012f27997", timestamp[], FALSE, result)
   IF NOT api_call_success THEN
      HALT
   ENDIF
   
   ; copy it to camera 
   BASE_DATA[1] = result
   
END
